* spicelib\examples\testfiles\all_components.asc
* Hand crafted to match the most important variants we encounter. 
* This file is not meant to be used in a simulation.

* Bxxx n001 n002 [VIRP]=<expression> [ic=<value>] ...
B1 NC_01 NC_02 V=1 tc1=2
B2 NC_01 NC_02 V=V(1) < {Vlow} ? {Vlow} : V(1) > {Vhigh} ? {Vhigh} : V(1) delay=1
B3 NC_01 NC_02 I=cos(v(1))+sin(v(2)) ic=1e-6 delay=10
B4 NC_01 NC_02 R=V(1) < 0? 2 : 1
B5 NC_01 NC_02 B=V(NC_01) VprXover=50mV

* Cxxx n1 n2 <capacitance> [ic=<value>] ...
* Cxxx n+ n- <value> <mname> <m=val> <scale=val> <temp=val> ...
* Cxxx n1 n2 C=<capacitance> [ic=<value>] ...
* Cxxx n1 n2 Q=<expression> [ic=<value>] [m=<value>] ...
C1 NC_03 NC_04 10µ rser=10 temp=60
C2 NC_03 NC_04 c = 10µF tc1 = 40
C3 NC_03 NC_04 c = 'V(cc) < {Vt} ? {C1} : {Ch}' tc1=-1e-03 tc2=1.3e-05
C4 NC_03 NC_04 1u*(4*atan(V(a,b)/4)*2+V(a,b))/3

* Dxxx anode cathode <model> [area] [off] [m=<val>] [n=<val>] [temp=<value>] ...
* Dxxx n+ n- mname <area=val> <m=val> <pj=val> <off> ...
D1 NC_05 NC_06 1N914
D2 NC_05 NC_06 1N4001 m=1 n=2

E1 NC_07 NC_08 NC_09 NC_10 1
F1 NC_11 NC_12 I1
G1 NC_13 NC_14 NC_15 NC_16 1
H1 NC_17 NC_18 I1
I1 NC_19 NC_20 1

* Jxxx D G S <model> [area] [off] [IC=Vds, Vgs] [temp=T]
J1 NC_21 NC_22 NC_23 2N3819
J2 NC_21 NC_22 NC_23 2N3819 ic=1, 2 temp=6

* Kxxx Lyyy Lzzz ... value
K1 L2 L3 1
K2 L2 L3 L4 L5 L6 0.1

* Lxxx n+ n- <value> <mname> <nt=val> <m=val> ...
* Lxxx n+ n- L = 'expression' <tc1=value> <tc2=value>
L1 NC_28 NC_29 1 temp=13
L2 NC_28 NC_29 1H
L3 NC_28 NC_29 "V(cc) < {Vt} ? {L1} : {L2}" temp=13

* Mxxx Nd Ng Ns Nb <model> [m=<value>] [L=<len>] ...
* Mxxx Nd Ng Ns <model> [L=<len>] [W=<width>]
M1 NC_30 NC_31 NC_32 NC_33 BSP89
M2 NC_30 NC_31 NC_32 NC_33 BSP89 temp=2
M3 Nd Ng Ns Si4410DY

O1 NC_34 NC_35 NC_36 NC_37 LTRA

* Qxxx nc nb ne <ns> <tj> mname <area=val> <areac=val> ...
* Qxxx Collector Base Emitter [Substrate Node] model [area] [off] [IC=<Vbe, Vce>] [temp=<T>]
Q1 NC_38 NC_39 NC_40 0 2N2222
Q2 NC_38 NC_39 NC_40 BC517 temp=60 ic=0.6, 5

* Rxxx n1 n2 <value> [tc=tc1, tc2, ...] [temp=<value>] ...
* Rxxx n+ n- <value> <mname> <l=length> <w=width> ...
* Rxxx n+ n- R = 'expression' <tc1=value> <tc2=value> <noisy=0> ...
R1 NC_41 NC_42 1 temp=12
R2 NC_41 NC_42 R=2k5R
R3 NC_41 NC_42 'V(cc) < {Vt} ? {R1} : {R2}' temp=13

* Sxxx n1 n2 nc+ nc- <model> [on,off]
S1 NC_43 NC_44 NC_45 NC_46 SW

T1 NC_47 NC_48 NC_49 NC_50 Td=50n Z0=50
U1 NC_51 NC_52 NC_53 URC
V1 NC_54 NC_55 1
V2 NC_08 NC_09 PWL(1u 0 +2n 1 +1m 1 +2n 0 +1m 0 +2n -1 +1m -1 +2n 0) AC 1 2 Rser=3 Cpar=4

* Wxxx n1 n2 Vnam <model> [on,off]
W1 NC_56 NC_57 V1 W on

X§U2 NC_58 NC_59 NC_60 NC_61 NC_62 AD549

* Zxxx D G S model [area] [m=<value>] [off] [IC=<Vds, Vgs>] [temp=<value>]
Z1 NC_63 NC_64 NC_65 NMF
Z2 NC_63 NC_64 NC_65 NMF ic=1, 2 area=1.4

.model D D
.lib standard.dio
.model NPN NPN
.model PNP PNP
.lib standard.bjt
.model NJF NJF
.model PJF PJF
.lib standard.jft
.model NMOS NMOS
.model PMOS PMOS
.lib standard.mos
* This is XU1
.lib ADI1.lib
.backanno
.end
